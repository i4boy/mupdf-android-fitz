apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.artifex.mupdf'
version = '1.26.3'

buildscript {
	repositories {
		if (project.hasProperty('MAVEN_REPO')) {
			maven { url MAVEN_REPO }
		} else {
			maven { url "file://${System.properties['user.home']}/MAVEN" }
		}
		google()
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:7.2.2'
	}
}

allprojects {
	repositories {
		if (project.hasProperty('MAVEN_REPO')) {
			maven { url MAVEN_REPO }
		} else {
			maven { url "file://${System.properties['user.home']}/MAVEN" }
		}
		google()
		mavenCentral()
	}
}

android {
	compileSdkVersion 33

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 34
		ndkVersion "29.0.13599879"
//		externalNativeBuild.ndkBuild.arguments '-j4'

		// Set ABI_FILTERS in your gradle.properties file
		if (project.hasProperty('ABI_FILTERS')) {
			ndk.abiFilters = []
			ndk.abiFilters.addAll(ABI_FILTERS.split(',').collect{it as String})
		}
	}
	publishing {
		singleVariant("release")
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs 'libmupdf/platform/java/src'
		}
	}

	externalNativeBuild {
		ndkBuild.path 'libmupdf/platform/java/Android.mk'
	}

}

apply from: 'p.config.gradle'
apply from: rootProject.file('publish.gradle')